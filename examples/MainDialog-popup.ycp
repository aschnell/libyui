// Example of using the Wizard widget.
//
// Note: YCP applications are discouraged from using the Wizard widget directly.
// Use the Wizard module instead.

{
    if ( ! UI::HasSpecialWidget(`Wizard) )
    {
	y2error( "This works only with UIs that provide the wizard widget!" );
	return;
    }

    string help_text =
	"<p>This is a help text.</p>"
	+ "<p>It should be helpful.</p>"
	+ "<p>If it isn't helpful, it should rather not be called a <i>help text</i>.</p>";

    UI::OpenDialog(`opt(`defaultsize ),
		   `VBox(
			 `Heading( "Main Dialog" ),
			 `VCenter( `ReplacePoint(`id(`contents), `Empty() ) ),
			 `Right(`PushButton(`id(`abort), "&Abort" ) )
			 )
		   );

    UI::ReplaceWidget( `contents,
		       `VBox(
			     `PushButton(`id(`doit1), "&Do Something" ),
			     `PushButton(`id(`doit2), "Do &More" ),
			     `PushButton(`id(`popup), "Popup &Wizard" )
			     )
		       );

    while( true )
    {
	map event = UI::WaitForEvent();

	y2milestone( "Got event: %1", event );
	
	symbol widget = (symbol) event[ "ID" ]:`nil;

	if ( widget == `abort )
	    break;

	if ( widget == `popup )
	{
	    UI::OpenDialog(`opt(`defaultsize ),
			   `VBox(
				 `VCenter( `ReplacePoint(`id(`contents), `Empty() ) ),
				 `Right(`PushButton(`id(`popdown), "Pop &Down" ) )
				 )
			   );

	    UI::ReplaceWidget( `contents,
			       `VBox(`id(`popupDialog),
				     `Heading("Popup Main Dialog"),
				     `VSpacing(2),
				     `PushButton(`id(`doit11), "&Do Something" ),
				     `PushButton(`id(`doit12), "Do &More" )
				     )
			       );
	}

	if ( widget == `popdown && UI::WidgetExists(`popupDialog) )
	    UI::CloseDialog();
    }

    UI::CloseDialog();
}
