{
list new_items =  [
		   `item(`id(1), "Mercedes",	60000),
		   `item(`id(2), "Audi",	50000),
		   `item(`id(3), "VW",		40000),
		   `item(`id(4), "BMW",		60000),
		   `item(`id(5), "Porsche",	80000)

];
list orig_items =  [
                    `item(`id(1), "Chili",		6),
                    `item(`id(2), "Salami Baguette",	nil),
                    `item(`id(3), "Spaghetti",	8),
                    `item(`id(4), "Steak Sandwich",	12)
                    ];

UI::OpenDialog(
	       `VBox(
		     `Heading("Today's menu"),
		     `MinSize( 30, 10,
			       `Table(`id(`menu), 
				      `opt(`notify, `multiSelection),
				      `header("Name", "Price"),
				      [
				       `item(`id(1), "Chili",		6),
				       `item(`id(2), "Salami Baguette",	nil),
				       `item(`id(3), "Spaghetti",	8),
				       `item(`id(4), "Steak Sandwich",	12)
				       ]
				      )
			       ),
                     `Label("Get notified on 'Return' or double click"),
                     `HBox(
                           `Label("Selected: "),
                           `TextEntry(`id(`info), "") 
		           ),
                     `HBox(
			   `PushButton(`id(`next), "Change &Table Contents"),
			   `PushButton(`id(`cancel), "&OK")
			   )
		     )
	       );

    UI::ChangeWidget(`menu, `SelectedItems, [ 1, 2 ]);
    list<integer> sel = (list<integer>)UI::QueryWidget(`menu, `SelectedItems);
    string selItems = "";
    foreach ( integer val, sel, 
              { 
                  selItems = selItems + " " + tostring(val);
              } );

    UI::ChangeWidget(`info, `Value, selItems );

    map event = $[];
    integer num = 0;
    repeat
    {
        string selItems = "";
        event = UI::WaitForEvent();
        
        if ( event["ID"]:nil == `menu )
        {
            sel = (list<integer>)UI::QueryWidget(`menu, `SelectedItems);
            foreach ( integer val, sel, 
                      { 
                          selItems = selItems + " " + tostring(val);
                      } );
            UI::ChangeWidget(`info, `Value, selItems ); 
        }
        else if ( event["ID"]:nil == `next )
        {
            num = num + 1;
            list items = [];
            if ( num%2 == 1 )
                items = new_items;
            else
                items = orig_items;
                
            // Change table contents
            UI::ChangeWidget(`menu, `Items, items );
            sel = (list<integer>)UI::QueryWidget(`menu, `SelectedItems);
            foreach ( integer val, sel, 
                      { 
                          selItems = selItems + " " + tostring(val);
                      } );
            UI::ChangeWidget(`info, `Value, selItems );
            
            // Double check: Retrieve contents and dump to log
            y2milestone( "New table content:\n%1", UI::QueryWidget(`menu, `Items ) );
        }
    } until ( event["ID"]:nil == `cancel );
    
    sel = (list<integer>)UI::QueryWidget(`menu, `SelectedItems);
    y2milestone( "Selected: %1", sel );
    
    UI::CloseDialog();

}

