// Demo for common ButtonBox-based dialogs
{
    void showDialog( term buttonBox )
    {
	boolean success = (boolean)
	    UI::OpenDialog(
			   `VBox(
				 `HVCenter( `Label( "Hello, World!" ) ),
				 buttonBox
				 )
			   );

	// Most YCP developers never use the return value of UI::OpenDialog().
	// Many of them probably don't even know that it has a return value.
	// 
	// Used properly, that return value can be used to recover from error
	// situations that would otherwise abort the program - like in this case.

	if ( success )
	{
	    UI::UserInput();
	    UI::CloseDialog();
	}
    }

    
    void okCancelDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok    ), "&OK" ),
			       `PushButton(`id(`cancel), "&Cancel" )
			       )
		    );
    }

    
    void yesNoDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`yes ), "&Yes" ),
			       `PushButton(`id(`no  ), "&No" )
			       )
		    );
    }

    
    void continueCancelDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`continue ), "C&ontinue" ),
			       `PushButton(`id(`cancel   ), "&Cancel" )
			       )
		    );
    }

    
    void okApplyCancelDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok    ), "&OK"     ),
			       `PushButton(`id(`apply ), "&Apply"  ),
			       `PushButton(`id(`cancel), "&Cancel" )
			       )
		    );
    }

    
    void okApplyCancelHelpDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok    ), "&OK"     ),
			       `PushButton(`id(`apply ), "&Apply"  ),
			       `PushButton(`id(`cancel), "&Cancel" ),
			       `PushButton(`id(`help  ), "&Help"   )
			       )
		    );
    }

    
    void okApplyCancelCustomHelpDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok      ), "&OK"     ),
			       `PushButton(`id(`apply   ), "&Apply"  ),
			       `PushButton(`id(`cancel  ), "&Cancel" ),
			       `PushButton(`id(`custom1 ), "&Do Something" ),
			       `PushButton(`id(`custom2 ), "Do &More" ),
			       `PushButton(`id(`help    ), "&Help"    )
			       )
		    );
    }

    
    void okDialog()
    {
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok    ), "&OK" )
			       )
		    );
    }


    void okRetryDialog()
    {
	// This will throw an error:
	// If there is more than one button in a button box, one of them must
	// have the [OK] role and one must have the [Cancel] role. 
	
	showDialog( `ButtonBox(
			       `PushButton(`id(`ok    ), "&OK" ),
			       `PushButton(`id(`retry ), "&Retry" )
			       )
		    );
    }

    
    void retryCancelDialog()
    {
	// Explicitly assigning the [OK] role to the [Retry] button
	
	showDialog( `ButtonBox(
			       `PushButton(`id(`retry ), `opt(`okButton), "&Retry" ),
			       `PushButton(`id(`cancel), "&Cancel" )
			       )
		    );
    }




    term buttons =
	`VBox(
	      `PushButton(`id(`okCancel       ), `opt(`hstretch), "&OK / Cancel" ),
	      `PushButton(`id(`yesNo          ), `opt(`hstretch), "&Yes / No" ),
	      `PushButton(`id(`continueCancel ), `opt(`hstretch), "C&ontinue / Cancel" ),
	      `PushButton(`id(`okApply        ), `opt(`hstretch), "OK / &Apply / Cancel" ),
	      `PushButton(`id(`okEtcHelp      ), `opt(`hstretch), "OK / Apply / Cancel / &Help" ),
	      `PushButton(`id(`okCustom       ), `opt(`hstretch), "OK / Apply / Cancel / C&ustom / Help" ),
	      `PushButton(`id(`okOnly         ), `opt(`hstretch), "O&K" ),
	      `PushButton(`id(`okRetry        ), `opt(`hstretch), "OK / Re&try (Error!)" ),
	      `PushButton(`id(`retryCancel    ), `opt(`hstretch), "&Retry / Cancel" )
	      );
    
    UI::OpenDialog(
		   `MarginBox( 1.0, 0.5,
			       `VBox(
				     `Heading( "ButtonBox Examples" ),
				     `VSpacing( 0.5 ),
				     `HVCenter( `HSquash( buttons ) ),
				     `VSpacing( 0.5 ),
				     `Right(`PushButton(`id(`close), "&Close" ) )
				     )
			       )
		   );

    symbol button = nil;

    repeat
    {
	button = (symbol) UI::UserInput();
	
	if ( button == `okCancel       )	okCancelDialog();
	if ( button == `yesNo	       )	yesNoDialog();
	if ( button == `continueCancel )	continueCancelDialog();
	if ( button == `okApply        )	okApplyCancelDialog();
	if ( button == `okEtcHelp      )	okApplyCancelHelpDialog();
	if ( button == `okCustom       )	okApplyCancelCustomHelpDialog();
	if ( button == `okOnly         )	okDialog();
	if ( button == `okRetry        )	okRetryDialog();
	if ( button == `retryCancel    )	retryCancelDialog();
	
    } until ( button == `close ||  // [Close] button
	      button == `cancel ); // WM_CLOSE

    UI::CloseDialog();
}
